Program                  ::= Expression*
                           | EOF

Expression               ::= Comment
                           | Block
                           | Operation
                           | Statement
                           | PreprocessorDirective
                           | 'void'

Block                    ::= '{' Expression* '}'

Operation                ::= VariableAssignment
VariableAssignment       ::= ConditionalExpression (('=' | '*=' | '/=' | '%=' | '+=' | '-=' | '<<=' | '>>=' | '&=' | '^=' | '|=') ConditionalExpression)*
ConditionalExpression    ::= LogicalORExpression ('?' VariableAssignment ':' VariableAssignment)?
LogicalORExpression      ::= LogicalANDExpression ('||' LogicalANDExpression)*
LogicalANDExpression     ::= BitwiseORExpression ('&&' BitwiseORExpression)*
BitwiseORExpression      ::= BitwiseXORExpression ('|' BitwiseXORExpression)*
BitwiseXORExpression     ::= BitwiseANDExpression ('^' BitwiseANDExpression)*
BitwiseANDExpression     ::= EqualityExpression ('&' EqualityExpression)*
EqualityExpression       ::= RelationalExpression (('==' | '!=') RelationalExpression)*
RelationalExpression     ::= ShiftExpression (('<' | '>' | '<=' | '>=') ShiftExpression)*
ShiftExpression          ::= AdditiveExpression (('<<' | '>>') AdditiveExpression)*
AdditiveExpression       ::= MultiplicativeExpression (('+' | '-') MultiplicativeExpression)*
MultiplicativeExpression ::= PowerExpression (('*' | '/' | '%') PowerExpression)*
PowerExpression          ::= UnaryExpression ('^' UnaryExpression)*
UnaryExpression          ::= Cast
                           | 'sizeof' Primary
                           | '++' Primary
                           | '--' Primary
                           | '+' Primary
                           | '-' Primary
                           | '~' Primary
                           | '!' Primary
                           | '&' Primary
                           | ('*')+ Primary
                           | Primary '++'
                           | Primary '--'
                           | Primary

Cast                     ::= '(' Type ')' Primary

Primary                  ::= Member
                           | Value
                           | ParenthesizedExpression

Statement                ::= Assembly
                           | Do
                           | While
                           | For
                           | Break
                           | Continue
                           | Goto
                           | Subroutine
                           | If
                           | Switch
                           | Enum
                           | Typedef
                           | Struct
                           | Union
                           | FunctionDeclaration
                           | VariableDeclaration
                           | Return
                           | EmptyStatement
                           
Assembly                 ::= 'asm' Qualifier* '(' String ')'
Do                       ::= 'do' Expression 'while' '(' Expression ')'
While                    ::= 'while' '(' Expression ')' Expression
For                      ::= 'for' '(' Expression ';' Expression ';' Expression ')' Expression
Break                    ::= 'break'
Continue                 ::= 'continue'
Goto                     ::= 'goto' Identifier
Subroutine               ::= ':'
If                       ::= 'if' '(' Expression ')' Expression Else?
Else                     ::= 'else' Expression
Switch                   ::= 'switch' '(' Expression ')' '{' Case+ Default? '}'
Case                     ::= 'case' Expression ':' Expression*
Default                  ::= 'default' ':' Expression*
Typedef                  ::= 'typedef' (Enum | Struct | Type | Union) (Identifier (',' Identifier)*)?
Enum                     ::= 'enum' Identifier? Block (Identifier (',' Identifier)*)?
Struct                   ::= 'struct' Identifier? Block (Identifier (',' Identifier)*)?
Union                    ::= 'union' Identifier? Block (Identifier (',' Identifier)*)?
FunctionDeclaration      ::= Qualifier* Type Identifier '(' Arguments ')' Block
VariableDeclaration      ::= Qualifier* Type Expression
Return                   ::= 'return' Expression
EmptyStatement           ::= ';'

Qualifier                ::= 'auto'
                           | 'const'
                           | 'extern'
                           | 'inline'
                           | 'long'
                           | 'register'
                           | 'restrict'
                           | 'short'
                           | 'signed'
                           | 'static'
                           | 'unsigned'
                           | 'volatile'
                           | '_Alignas'
                           | '_Alignof'
                           | '_Atomic'
                           | '_Bool'
                           | '_Complex'
                           | '_Generic'
                           | '_Imaginary'
                           | '_Noreturn'
                           | '_Static_assert'
                           | '_Thread_local'

Type                     ::= DataType ('*')*
                           | Identifier ('*')*
DataType                 ::= 'char'
                           | 'double'
                           | 'float'
                           | 'int'
                           | 'long'
                           | 'short'
                           | 'void'
                           | '_Bool'
                           | '_Complex'
                           
Arguments                ::= Expression (',' Expression)*

Member                   ::= Identifier (('.' | '->') Identifier)* '(' Arguments? ')'
                           | Identifier (('.' | '->') Identifier)* ('[' Arguments? ']')*

Array                    ::= '{' Element (',' Element)* '}'
Matrix                   ::= '[' Row? (';' Row)* ']'

Element                  ::= (Key ':')? Expression
Key                      ::= Identifier
                           | String

Row                      ::= Column (',' Column)*
Column                   ::= Expression

ParenthesizedExpression  ::= '(' Expression ')'

Value                    ::= Integer
                           | Real
                           | Character
                           | String
                           | Array
                           | Matrix
                           | Null
                           | False
                           | True

PreprocessorDirective    ::= DefineDirective
                           | UndefDirective
                           | ErrorDirective
                           | IfDirectiveStatement
                           | IncludeDirective
                           | LineDirective
                           | PragmaDirective
                           | EmptyDirective
                           
DefineDirective          ::= '#define' Identifier Expression
                           | '#define' Identifier '(' Arguments? ')' Expression
UndefDirective           ::= '#undef' Identifier
ErrorDirective           ::= '#error' String
IfDirectiveStatement     ::= (IfDirective | IfdefDirective | IfndefDirective) ElifDirective* ElseDirective? EndifDirective
IfDirective              ::= '#if' Expression*
IfdefDirective           ::= '#ifdef' Identifier Expression*
IfndefDirective          ::= '#ifndef' Identifier Expression*
ElifDirective            ::= '#elif' Expression Expression*
ElseDirective            ::= '#else' Expression*
EndifDirective           ::= '#endif'
IncludeDirective         ::= '#include' String
                           | '#include' '<' Expression '>'
LineDirective            ::= '#line' Integer String?
PragmaDirective          ::= '#pragma' Expression*
EmptyDirective           ::= '#'

WhiteSpace               ::= WhiteSpace^token
                         /* ws: definition */

<?TOKENS?>

EOF                      ::= $

Identifier               ::= (FirstIdentifierChar (IdentifierChar)*) - ReservedWord

ReservedWord             ::= 'asm'
                           | 'auto'
                           | 'break'
                           | 'case'
                           | 'char'
                           | 'const'
                           | 'continue'
                           | 'default'
                           | 'do'
                           | 'double'
                           | 'else'
                           | 'enum'
                           | 'extern'
                           | 'false'
                           | 'float'
                           | 'for'
                           | 'goto'
                           | 'if'
                           | 'inline'
                           | 'int'
                           | 'long'
                           | 'null'
                           | 'register'
                           | 'restrict'
                           | 'return'
                           | 'short'
                           | 'signed'
                           | 'sizeof'
                           | 'static'
                           | 'struct'
                           | 'switch'
                           | 'true'
                           | 'typedef'
                           | 'union'
                           | 'unsigned'
                           | 'void'
                           | 'volatile'
                           | 'while'
                           | '_Alignas'
                           | '_Alignof'
                           | '_Atomic'
                           | '_Bool'
                           | '_Complex'
                           | '_Generic'
                           | '_Imaginary'
                           | '_Noreturn'
                           | '_Static_assert'
                           | '_Thread_local'

Null                     ::= 'null'
True                     ::= 'true'
False                    ::= 'false'

FirstIdentifierChar      ::= '_' | [A-Za-z]
IdentifierChar           ::= '_' | [0-9] | [A-Za-z]

Character                ::= "'" SingleCharacter "'"
                           | "'" EscapeSequence "'"
SingleCharacter          ::= InputCharacter - ( "'" | '\' )

String                   ::= '"' StringCharacters? '"'
StringCharacters         ::= StringCharacter+
StringCharacter          ::= InputCharacter - ( '"' | '\' )
                           | EscapeSequence
EscapeSequence           ::= BackSlash 'b'
                           | BackSlash 't'
                           | BackSlash 'n'
                           | BackSlash 'f'
                           | BackSlash 'r'
                           | BackSlash '"'
                           | BackSlash "'"
                           | BackSlash BackSlash
                           | OctalEscape
                           | HexEscapeSequence
OctalEscape              ::= '\' OctalDigit+
HexEscapeSequence        ::= '\x' HexDigit+
OctalDigit               ::= '0'
                           | '1'
                           | '2'
                           | '3'
                           | '4'
                           | '5'
                           | '6'
                           | '7'
HexDigit                 ::= [0-9a-fA-F]
BackSlash                ::= '\' ( UnicodeMarker '005' [cC] )?
InputCharacter           ::= UnicodeInputCharacter - ( CR | LF )

UnicodeInputCharacter    ::= UnicodeEscape
                           | RawInputCharacter
UnicodeEscape            ::= '\' UnicodeMarker HexDigit HexDigit HexDigit HexDigit
UnicodeMarker            ::= 'u' | 'U'
RawInputCharacter        ::= [#x0001-#xD7FF]
                           | [#xE000-#xFFFD]
                           | [#x10000-#x10FFFF]

Integer                  ::= '0'
                           | Digit+ ('u' | 'U')? ('l' | 'll' | 'L' | 'LL' | 'f' | 'F')?
                           | Octal
                           | Hexadecimal
Real                     ::= '-'? Digit+ '.' Digit+ (('e' | 'E' | 'p' | 'P') ('+' | '-')? Digit+)?

Digit                    ::= [0-9]
Octal                    ::= '0' OctalDigit+
Hexadecimal              ::= '0x' HexDigit+

Comment                  ::= SingleLineComment

SingleLineComment        ::= '//' (SourceCharacter - LineTerminator)*

SourceCharacter          ::= .

LineTerminator           ::= LF
                           | CR
                           | CR LF
LF                       ::= #x000A
CR                       ::= #x000D

WhiteSpace               ::= [#x0009#x000A#x000D#x0020]+
